# Ordinary Differential Equations  {#ode-solver.chapter}

Stan provides a built-in mechanism for specifying and solving systems
of ordinary differential equations (ODEs).  Stan provides two
different integrators, one tuned for solving non-stiff systems and one
for stiff systems.

* `rk45`: a fourth and fifth order Runge-Kutta method for
  non-stiff systems [@DormandPrince:1980; @AhnertMulansky:2011], and

* `bdf`: a variable-step, variable-order,
  backward-differentiation formula implementation for stiff systems
  [@CohenHindmarsh:1996; @SerbanHindmarsh:2005]

For a discussion of stiff ODE systems, see the [stiff ODE
section](#stiff-ode.section).  In a nutshell, the stiff solvers are
slower, but more robust; how much so depends on the system and the
region of parameter space.  The function signatures for Stan's ODE
solvers can be found in the reference manual section on ODE solvers.


## Example: Simple Harmonic Oscillator

As an example of a system of ODEs, consider a harmonic
oscillator, which is characterized by an equilibrium position and a
restoring force proportional to the displacement with friction.
The system state will be a pair $y = (y_1, y_2)$ representing position
and momentum: a point in phase space.  The change in the system
with respect to time is given by the following differential equations.^[This example is drawn from the documentation for the Boost Numeric Odeint library [@AhnertMulansky:2011], which Stan uses to implement the `rk45` solver.]

$$
\frac{d}{dt} y_1 = y_2
\ \ \ \ \ \ \ \ \ \ \
\frac{d}{dt} y_2 = -y_1 - \theta y_2
$$
id:ode-sho.equation

The state equations implicitly define the system state at a given time
as a function of an initial state, elapsed time since the initial
state, and the system parameters.

### Solutions Given Initial Conditions {-}

Given a value of the system parameter $\theta$ and an initial state
$y(t_0)$ at time $t_0$, it is possible to simulate the evolution of
the solution numerically in order to calculate $y(t)$ for a specified
sequence of times $t_0 < t_1 < t_2 < \cdots$.

## Coding an ODE System

A system of ODEs is coded directly in Stan as a function with a
strictly specified signature.  For example, the simple harmonic
oscillator can be coded using the following function in Stan (see the
[user-defined functions chapter](#functions-programming.chapter) for
more information on coding user-defined functions).

```
real[] sho(real t,        // time
           real[] y,      // state
           real[] theta,  // parameters
           real[] x_r,    // data (real)
           int[] x_i) {   // data (integer)
  real dydt[2];
  dydt[1] = y[2];
  dydt[2] = -y[1] - theta[1] * y[2];
  return dydt;
}
```

The function takes in a time `t` (a real value), a a system state
`y` (real array), system parameters `theta` (a real array),
along with real data in variable `x_r` (a real array) and
integer data in variable `x_i` (an integer array).  The system
function returns the array of derivatives of the system state with
respect to time, evaluated at time `t` and state `y`.  The
simple harmonic oscillator coded here does not have time-sensitive
equations; that is, `t` does not show up in the definition of
`dydt`.  The simple harmonic oscillator does not use real or
integer data, either.  Nevertheless, these unused arguments must be
included as arguments in the system function with exactly the
signature shown above.


### Strict Signature {-}

The function defining the system must have exactly these argument
types and return type.  This may require passing in zero-length arrays
for data or parameters if the system does not involve data or
parameters.  A full example for the simple harmonic oscillator, which
does not depend on any constant data variables, is provided in the
simple harmonic oscillator trajectory plot. 

### Discontinuous ODE System Function {-}

The ODE integrator is able to integrate over discontinuities in the
state function, although the accuracy of points near the discontinuity
may be problematic (requiring many small steps).  An example of such a
discontinuity is a lag in a pharmacokinetic model, where a
concentration is going to be zero for times $0 < t < t'$ for some
lag-time $t'$, whereas it will be nonzero for times $t \geq t'$.  As
an example, would involve code in the system such as

```
if (t < t_lag)
  return 0;
else
  ... return non-zero value...;
```


### Varying Initial Time {-}

Stan's ODE solvers require the initial time argument to be a constant
(i.e., a function of data or transformed data variables and
constants).  This means that, in general, there's no way to use the
`integrate_ode` function to accept a parameter for the initial
time and thus no way in general to estimate the initial time of an ODE
system from measurements.

## Solving a System of Linear ODEs using a Matrix Exponential

The solution to $\frac{d}{dt} y = ay$ is $y = y_0e^{at}$, where the constant
$y_0$ is determined by boundary conditions. We can extend this solution
to the vector case:

$$
\frac{d}{dt}y = A y
$$
id:ode.linODEs

where $y$ is now a vector of length $n$ and $A$ is an $n$ by $n$ matrix. The
solution is then given by:

$$
y = e^{tA}y_0
$$
id:ode.linOEs.sln

where the matrix exponential is formally defined by the convergent power series:

$$
e^{tA} = \sum_{n=0}^{\infty} \dfrac{tA^n}{n!} = I + tA + \frac{t^2A^2}{2!} + ...
$$
id:ode.matrix_exp.def

We can apply this technique to the simple harmonic oscillator example, by
setting

$$
  y = \left[\begin{array}{c}
        y_1 \\
        y_2 \\
        \end{array}\right] \ \ \ \ \
   A = \left[\begin{array}{cc}
	0 & 1 \\
	-1 & -\theta \\
	\end{array}\right]
$$
id:ode.sho_matrix

The Stan model to simulate noisy observations using a matrix exponential function
is given below. Because we are performing matrix
operations, we declare `y0` and `y_hat` as vectors, instead of using arrays,
as in the previous example code.

In general, computing a matrix exponential will be more efficient than using a numerical
solver. We can however only apply this technique to systems of \underline{linear} ODEs.

```
data {
  int<lower=1> T;
  vector[2] y0;
  real ts[T];
  real theta[1];
}
model {
}
generated quantities {
  vector[2] y_hat[T];
  matrix[2, 2] A = [[ 0,  1],
                    [-1, -theta[1]]]
  for (t in 1:T)
    y_hat[t] = matrix_exp((t - 1) * A) * y0;
  // add measurement error
  for (t in 1:T) {
    y_hat[t, 1] += normal_rng(0, 0.1);
    y_hat[t, 2] += normal_rng(0, 0.1);
  }
}
```
id:sho-sim-me.figure

This Stan program simulates noisy measurements from a simple harmonic
oscillator.  The system of linear differential equations is coded as a
matrix. The system parameters `theta` and initial state `y0` are read
in as data along observation times `ts`.  The generated quantities
block is used to solve the ODE for the specified times and then add
random measurement error, producing observations `y_hat`. Because the
ODEs are linear, we can use the `matrix_exp` function to solve the
system.



## Measurement Error Models

Statistical models or differential equations may be used to estimate
the parameters and/or initial state of a dynamic system given noisy
measurements of the system state at a finite number of time points.

For instance, suppose the simple harmonic oscillator has a parameter
value of $\theta = 0.15$ and initial state $y(t=0) = (1,0)$.  Now
suppose the system is observed at 10 time points, say $t=1, 2, ...,
10$, where each measurement of $y(t)$ has independent
$\mathsf{normal}(0, 0.1)$ error in both dimensions ($y_1(t)$ and
$y_2(t)$).  A plot of such measurements is shown in the simple harmonic
oscillator trajectory plots.

Trajectory of the simple harmonic oscillator given parameter $\theta=0.15$ and initial condition $y(t=0) = (1,0)$ with additional independent $\mathsf{normal}(0,0.1)$ measurement error in both dimensions.

```{r fig.cap="Simple harmonic oscillator trajectory"}
knitr::include_graphics("img/sho-ode-trajectory.pdf")
```
id:sho-trajectory.figure


### Simulating Noisy Measurements {-}

The data used to make this plot is derived from the Stan model to
simulate noisy observations given below.

```
functions {
  real[] sho(real t,
             real[] y,
             real[] theta,
             real[] x_r,
             int[] x_i) {
    real dydt[2];
    dydt[1] = y[2];
    dydt[2] = -y[1] - theta[1] * y[2];
    return dydt;
  }
}
data {
  int<lower=1> T;
  real y0[2];
  real t0;
  real ts[T];
  real theta[1];
}
transformed data {
  real x_r[0];
  int x_i[0];
}
model {
}
generated quantities {
  real y_hat[T,2] = integrate_ode_rk45(sho, y0, t0, ts, theta, x_r, x_i);
  // add measurement error
  for (t in 1:T) {
    y_hat[t, 1] += normal_rng(0, 0.1);
    y_hat[t, 2] += normal_rng(0, 0.1);
  }
}
```
id:sho-sim.figure

The system of differential equations is coded as a function.  The
system parameters `theta` and initial state `y0` are read in as data
along with the initial time `t0` and observation times `ts`. The
generated quantities block is used to solve the ODE for the specified
times and then add random measurement error, producing observations
`y_hat`.  Because the system is not stiff, the `rk45` solver is used.


This program illustrates the way in which the ODE solver is called in
a Stan program,

```
y_hat = integrate_ode_rk45(sho, y0, t0, ts, theta, x_r, x_i);
```

This assigns the solutions to the system defined by function
`sho`, given initial state `y0`, initial time `t0`,
requested solution times `ts`, parameters `theta`, real data
`x`, and integer data `x_int`.  The call explicitly
specifies the Runge-Kutta solver (for non-stiff systems).

Here, the ODE solver is called in the generated quantities block to
provide a $10 \times 2$ array of solutions `y_hat` to
which measurement error is added using the normal pseudo-random number
generating function `normal_rng`.  The number of rows in the
solution array is the same as the size of `ts`, the requested
solution times.

### Data versus Parameters {-}

Unlike other functions, the integration functions for ODEs are limited
as to the origins of variables in their arguments.  In particular, the
time `t`, real data `x`, and integer data `x_int` must
be expressions that only involve data or transformed data variables.
The initial state `y` or the parameters `theta` are the only
arguments which may involve parameters.


### Estimating System Parameters and Initial State {-}

Stan provides statistical inference for unknown initial states and/or
parameters.  The ODE solver will be used deterministically to produce
predictions, much like the linear predictor does in a generalized
linear model.  These states will then be observed with measurement error.


```
functions {
  real[] sho(real t,
             real[] y,
             real[] theta,
             real[] x_r,
             int[] x_i) {
    real dydt[2];
    dydt[1] = y[2];
    dydt[2] = -y[1] - theta[1] * y[2];
    return dydt;
  }
}
data {
  int<lower=1> T;
  real y[T,2];
  real t0;
  real ts[T];
}
transformed data {
  real x_r[0];
  int x_i[0];
}
parameters {
  real y0[2];
  vector<lower=0>[2] sigma;
  real theta[1];
}
model {
  real y_hat[T,2];
  sigma ~ cauchy(0, 2.5);
  theta ~ std_normal();
  y0 ~ std_normal();
  y_hat = integrate_ode_rk45(sho, y0, t0, ts, theta, x_r, x_i);
  for (t in 1:T)
    y[t] ~ normal(y_hat[t], sigma);
}
```
id:sho-both.figure

This Stan program allows estimates of unknown initial conditions `y0`
and system parameter `theta` for the simple harmonic oscillator with
independent normal measurement error.

A Stan program that can be used to estimate both the initial state and
parameter value for the simple harmonic oscillator given noisy
observations is given above.  Compared to the program for simulation,
the program to estimate parameters uses the `integrate_ode` function
in the model block rather than the generated quantities block.  There
are Cauchy priors on the measurement error scales `sigma` and standard
normal priors on the components of parameter array `theta` and initial
state parameter array `y0`.  The solutions to the ODE are then
assigned to an array `y_hat`, which is then used as the location in
the observation noise model as follows.

```
y_hat = integrate_ode_rk45(sho, y0, t0, ts, theta, x_r, x_i);
for (t in 1:T)
  y[t] ~ normal(y_hat[t], sigma);
```

As with other regression-like models, it's easy to change the noise
model to be robust (e.g., Student-t distributed), to be correlated in
the state variables (e.g., with a multivariate normal distribution),
or both (e.g., with a multivariate Student-t distribution).

In this simple model with independent noise scales of 0.10, 10
observed data points for times $t = 1, ..., 10$ is sufficient to
reliably estimate the ODE parameter, initial state, and noise scales.


## Stiff ODEs {#stiff-ode.section}

A stiff system of ordinary differential equations can be roughly
characterized as systems presenting numerical difficulties for
gradient-based stepwise solvers.  Stiffness typically arises due to
varying curvature in the dimensions of the state, for instance one
component evolving orders of magnitude more slowly than another.^[Not coincidentally, high curvature in the posterior of a general Stan model poses the same kind of problem for Euclidean Hamiltonian Monte Carlo (HMC) sampling.  The reason is that HMC is based on the leapfrog algorithm, a gradient-based, stepwise numerical differential equation solver specialized for Hamiltonian systems with separable potential and kinetic energy terms.]


Stan provides a specialized solver for stiff ODEs
[@CohenHindmarsh:1996; @SerbanHindmarsh:2005].  An ODE system is
specified exactly the same way with a function of exactly the same
signature.  The only difference is in the call to the integrator for
the solution; the `rk45` suffix is replaced with `bdf`, as in

```
y_hat = integrate_ode_bdf(sho, y0, t0, ts, theta, x_r, x_i);
```


Using the stiff (`bdf`) integrator on a system that is not stiff
may be much slower than using the non-stiff (`rk45`) integrator;
this is because it computes additional Jacobians to guide the
integrator.  On the other hand, attempting to use the non-stiff
integrator for a stiff system will fail due to requiring a small step
size and too many steps.

## Control Parameters for ODE Solving

The calls to the integrators shown above just used the default
control settings.  Both the non-stiff and stiff integrators allow
three additional arguments, all of which must be supplied if any of
them is required.

```
y_hat = integrate_ode_bdf(sho, y0, t0, ts, theta, x_r, x_i,
                          rel_tol, abs_tol, max_steps);
```

The three control arguments are relative tolerance, absolute
tolerance, and maximum number of steps.   The default values for
relative and absolute tolerance are both `1e-6` ($10^{-6}$), and
the default maximum number of steps is `1e6` ($10^6$).

### Data only for control parameters {-}

The control parameters must be data variables---they can not be
parameters or expressions that depend on parameters, including local
variables in any block other than transformed data and generated
quantities.  User-defined function arguments may be qualified as only
allowing data arguments using the `data` qualifier.

### Tolerance {-}

The relative and absolute tolerance control the accuracy of the
solutions generated by the integrator.  Relative tolerances are
relative to the solution value, whereas absolute tolerances is the
maximum absolute error allowed in a solution.

Smaller tolerances produce more accurate solutions.  Smaller
tolerances also require more computation time.

#### Sensitivity Analysis {-}

The tolerances should be set low enough that setting them lower does
not change the statistical properties of posterior samples generated
by the Stan program.

### Maximum Number of Steps {-}

The maximum number of steps can be used to stop a runaway simulation.
This can arise in MCMC when a bad jump is taken, particularly during
warmup.  With the non-stiff solver, this may result in jumping into a
stiff region of the parameter space, which would require a  small
step size and  many steps to satisfy even modest tolerances.
